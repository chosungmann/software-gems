;	CP/M 1975 boot-loader for Z80-Simulator
;
;	Copyright (C) 2007 by Udo Munk
;
; This old version of CP/M doesn't use any vectors in page zero.
; The system has a boot routine that just jumps to 0000H, so the
; boot loader has to stay in place. We have to keep it as small
; as possible, memory up to 004FH can be used but no more.
;
; This is the Z80 version to be assembled with z80asm.
;
	ORG	0		;mem base of boot
;
CCP	EQU	2900H		;base of the ccp
SECTS	EQU	51		;# of sectors to load (26 * 2 - 1)
;
;	I/O ports
;
DRIVE   EQU	10		;fdc-port: # of drive
TRACK   EQU	11		;fdc-port: # of track
SECTOR  EQU	12		;fdc-port: # of sector
FDCOP   EQU	13		;fdc-port: command
FDCST   EQU	14		;fdc-port: status
DMAL    EQU	15		;dma-port: dma address low
DMAH    EQU	16		;dma-port: dma address high
;
	JP	COLD
;
; Place a jump to the BDOS entry at 0005H, so that we can run
; programs from CP/M 1.3 and 1.4
;
	ORG	5
	JP	3206H
;
;	begin the load operation
;
COLD:	LD	BC,2		;b=track 0, c=sector 2
	LD	D,SECTS		;d=# sectors to load
	LD	HL,CCP		;base transfer address
	XOR	A		;select drive A
	OUT	(DRIVE),A
;
;	load the next sector
;
LSECT:	LD	A,B		;set track
	OUT	(TRACK),A
	LD	A,C		;set sector
	OUT	(SECTOR),A
	LD	A,L		;set dma address low
	OUT	(DMAL),A
	LD	A,H		;set dma address high
	OUT	(DMAH),A
	XOR	A		;read sector
	OUT	(FDCOP),A
	IN	A,(FDCST)	;get status of fdc
	OR	A		;read successful ?
	JP	Z,CONT		;yes, continue
	DI
	HALT			;no, halt cpu
CONT:
				;go to next sector if load is incomplete
	DEC	D		;sects=sects-1
	JP	Z,CCP		;head for the ccp
;
;	more sectors to load
;
;	we aren't using a stack, so use <sp> as scratch register
;	to hold the load address increment
;
	LD	SP,128		;128 bytes per sector
	ADD	HL,SP		;<hl> = <hl> + 128
;
	INC	C		;sector = sector + 1
	LD	A,C
	CP	27		;last sector of track ?
	JP	C,LSECT		;no, go read another
;
;	end of track, increment to next track
;
	LD	C,1		;sector = 1
	INC	B		;track = track + 1
	JP	LSECT		;for another group
;
	END			;of boot loader
